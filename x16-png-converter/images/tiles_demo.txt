100 REM LOAD BINARY FILES
110 VLOAD "TILES.BIN",8,0,$4000
120 VLOAD "TILES-PALETTE.BIN",8,1,$FA00
130 REM
140 REM SETUP SCREEN
150 SCREEN 3:REM SET SCREEN TO 320X240
160 POKE $9F29,16+1:REM SWITCH TO LAYER 0, VGA OUTPUT
170 POKE $9F2D,1:REM MAP HEIGHT=32, MAP WIDTH=32, BPP=2
180 POKE $9F2E,16+8:REM MAP BASE=$3000
190 POKE $9F2F,32+3:REM TILE BASE=$4000, TILE HEIGHT=16, TILE WIDTH=16
200 REM
210 REM CREATE MAP
220 I=0
230 FOR ROW=0 TO 31
240 FOR COL=0 TO 31
250 ADDR=$3000+ROW*64+COL*2
260 VPOKE 0,ADDR,I:REM TILE INDEX
270 VPOKE 0,ADDR+1,0:REM PALETTE INDEX 0, NO FLIPS
280 I=I+1:IF I>23 THEN I=0
290 NEXT COL
300 NEXT ROW
310 GET A$:IF A$="" THEN 310
320 REM
330 REM RESTORE SCREEN
340 SCREEN 0:REM SET SCREEN TO 640X480
350 COLOR 1,6:REM FG = WHITE, BG = BLUE
360 POKE $9F29,32+1: REM SWITCH BACK TO LAYER 1, VGA OUTPUT
370 REM
380 REM RESTORE CHAR SET IN CASE IT HAS BEEN OVERWRITTEN
390 PRINT CHR$($8E):REM SET UPPER CASE WHICH CAUSES UPLOAD OF CHAR SET
400 REM
410 REM RESTORE FIRST 16 COLORS
420 FOR I = 0 TO 31
430 READ C
440 VPOKE 1,$FA00+I,C
450 NEXT
460 CLS
470 REM ORIGINAL PALETTE
480 DATA $00,$00,$FF,$0F,$00,$08,$FE,$0A,$4C,$0C,$C5,$00,$0A,$00,$E7,$0E
490 DATA $85,$0D,$40,$06,$77,$0F,$33,$03,$77,$07,$F6,$0A,$8F,$00,$BB,$0B
